plugins {
    id 'java'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
}

apply plugin: "org.javamodularity.moduleplugin"

group = 'org.firedragon91245.spi'
project.version = releaseVersion

sourceSets {
    testapp {
        java {
            srcDir 'src/testapp/java'
        }
        resources {
            srcDir 'src/testapp/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register('runTestApp', JavaExec) {
    group = 'application'
    description = 'Runs the test application to test the SPI implementation.'

    classpath = sourceSets.testapp.runtimeClasspath
    mainClass.set('org.firedragon91245.spi.testapp.Main')

    standardInput = System.in
    standardOutput = System.out
    errorOutput = System.err

    // Capture logs appropriately
    logging.captureStandardOutput LogLevel.LIFECYCLE
    logging.captureStandardError LogLevel.ERROR
}

tasks.register('testappJar', Jar) {
    group = 'build'
    description = 'Builds the test application as a JAR.'

    from sourceSets.testapp.output
    archiveBaseName.set("${project.name}-testapp-${releaseVersion}")
    destinationDirectory.set(file("${layout.buildDirectory}/libs"))

    manifest {
        attributes(
                'Implementation-Title': "${project.name} TestApp",
                'Implementation-Version': releaseVersion
        )
    }
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

configurations {
    testappImplementation
    testappRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("com.googlecode.soundlibs:jorbis:0.0.17.4")
    implementation("com.googlecode.soundlibs:tritonus-share:0.3.7.4")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

jar {
    from sourceSets.main.output
    archiveBaseName = "${project.name}-${releaseVersion}"
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': releaseVersion
        )
    }
}

test {
    useJUnitPlatform()
}